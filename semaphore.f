\ include gpio.f
\ inlcude time.f
\ inlcude lcd.f

DECIMAL

20 CONSTANT RCAR
21 CONSTANT YCAR
26 CONSTANT GCAR

19 CONSTANT RPED
13 CONSTANT YPED
6  CONSTANT GPED

5  CONSTANT BUTTON

: RED RCAR RPED ;
: YELLOW YCAR YPED ;
: GREEN GCAR GPED ;

: CAR DROP ;
: PEDESTRIAN SWAP DROP ;

\ SEMSETUP ( -- ) set the output state to the semaphore's GPIO.
: SEMSETUP 
    RED CAR GPFSELOUT!
    YELLOW CAR GPFSELOUT!
    GREEN CAR GPFSELOUT!
    RED PEDESTRIAN GPFSELOUT!
    YELLOW PEDESTRIAN GPFSELOUT!
    GREEN PEDESTRIAN GPFSELOUT!
;

\ SEMINIT ( -- ) initializes the state of the semaphore's LEDs.
: SEMINIT  
    RED CAR GPOFF! 
    YELLOW CAR GPOFF! 
    GREEN CAR GPON! 
    RED PEDESTRIAN GPON!
    YELLOW PEDESTRIAN GPOFF! 
    GREEN PEDESTRIAN GPOFF! 
;

\ DELAYANDPRINT ( n -- ) waits for n and prints second to lcd
: DELAYANDPRINT 
    NOW + 
    
    BEGIN DUP NOW - DUP 

        0> IF 

            DUP TOSEC 
            
            DUP 10 < IF  1 2 LCDLN! LCDSPACE THEN

            2 2 LCDNUMBER 
            
            S" sec. " 4 2 LCDSTRING  
        THEN
            
    0 <= UNTIL DROP 
;

\ SEMAPHORE_ROUTINE ( -- ) run the semaphore routine when button is pressed
: SEMAPHORE_ROUTINE 
    LCDCLR S" CAR YELLOW " 0 1 LCDSTRING
    GREEN CAR GPOFF! YELLOW CAR GPON! 5 SEC DELAYANDPRINT 
    LCDCLR S" PED. GREEN " 0 1 LCDSTRING
    RED CAR GPON! YELLOW CAR GPOFF! RED PEDESTRIAN GPOFF! GREEN PEDESTRIAN GPON! 10 SEC DELAYANDPRINT  
    LCDCLR S" PED. YELLOW " 0 1 LCDSTRING
    GREEN PEDESTRIAN GPOFF! YELLOW PEDESTRIAN GPON! 5 SEC DELAYANDPRINT  
    LCDCLR S" PED. RED " 0 1 LCDSTRING
    RED PEDESTRIAN GPON! YELLOW PEDESTRIAN GPOFF! RED CAR OFF GREEN CAR ON
    LCDCLR S" PED. CAN CALL IN " 0 1 LCDSTRING
    10 SEC DELAYANDPRINT
;

\ CHECK ( n -- ) busy wait until GPIO level passed as input is up and then start SEMAPHORE_ROUTINE
: CHECK LCDCLR S" PED. CAN CALL " 0 1 LCDSTRING BEGIN DUP GPLEV@ 0= UNTIL DROP SEMAPHORE_ROUTINE ;

\ MAIN ( -- ) Recursive main routine of semaphore
: MAIN SEMINIT BUTTON CHECK MAIN ;

SEMSETUP
LCDSETUP
LCDINIT
