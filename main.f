VARIABLE LASTRUN
VARIABLE STARTTIME

VARIABLE ACTION_IDX
VARIABLE ACTION_LEN

VARIABLE FULLSEMLEN

VARIABLE ACTIONS 

DECIMAL

5 CELLS ALLOT ACTIONS !

: ACTION CELLS ACTIONS @ + ;

: FULLSEMLEN-- 1 FULLSEMLEN -! ;

: ACTION_IDX+ ACTION_IDX +! ;
: ACTION_LEN! ACTION_LEN ! ;

: ACTION_LEN-- 1 ACTION_LEN -! ;

: ISCHED! 1 SEC SYSC1! ;

: NEXTACTION! ACTION_IDX+ ACTION_LEN! ISCHED! ;

: 0ACTION LCDCLR S" P. RED CAN CALL " 0 1 LCDSTRING 30 FULLSEMLEN ! ;

: 1ACTION S" CAR YELLOW       " 0 1 LCDSTRING
    GREEN CAR GPOFF! YELLOW CAR GPON! 
    5 1 NEXTACTION!
;
: 2ACTION S" P. GREEN    " 0 1 LCDSTRING
    RED CAR GPON! YELLOW CAR GPOFF! RED PEDESTRIAN GPOFF! GREEN PEDESTRIAN GPON!
    19 1 NEXTACTION!
;

: 3ACTION S" P. YELLOW " 0 1 LCDSTRING
    GREEN PEDESTRIAN GPOFF! YELLOW PEDESTRIAN GPON!
    4 1 NEXTACTION!
;

: 4ACTION S" P. RED WAIT " 0 1 LCDSTRING
    RED PEDESTRIAN GPON! YELLOW PEDESTRIAN GPOFF! RED CAR OFF GREEN CAR ON
    10 FULLSEMLEN !
    9 -4 NEXTACTION!
;

: EXEC_NEXT ACTION_IDX @ ACTION @ EXECUTE ;

: SET0 ' 0ACTION 0 ACTION ! ;
: SET1 ' 1ACTION 1 ACTION ! ;
: SET2 ' 2ACTION 2 ACTION ! ;
: SET3 ' 3ACTION 3 ACTION ! ;
: SET4 ' 4ACTION 4 ACTION ! ;

: TIMER_ISR 
    FULLSEMLEN @ 10 < IF  1 2 LCDLN! LCDSPACE THEN
    FULLSEMLEN @ 2 2 LCDNUMBER
    ACTION_LEN @ 
    0= IF EXEC_NEXT 
    ELSE ACTION_LEN-- ISCHED! THEN
    FULLSEMLEN--
;

: GPIO_ISR ACTION_IDX @ 0= IF 1 ACTION_IDX+ EXEC_NEXT THEN ;

: TIMER_ISR! ' TIMER_ISR 'TIMER_ISR ! ;
: GPIO_ISR! ' GPIO_ISR 'GPIO_ISR ! ;

SET0
SET1
SET2
SET3
SET4

TIMER_ISR!
GPIO_ISR!
IRQHANDLER!

SEMSETUP
LCDSETUP
SEMINIT
LCDINIT

0 ACTION_IDX !
40 FULLSEMLEN !

5 GPAFEN!
IRQGPIO!
IRQTIMER1!
+IRQ

: MAINLOOP 
    NOW STARTTIME !
    STARTTIME @ ITS !
    STARTTIME @ LASTRUN !
    EXEC_NEXT
    BEGIN
        NOW STARTTIME @ - 1 MIN < WHILE
            LASTRUN @ ITS @ <> IF ITS @ LASTRUN ! 'GENERIC_ISR @ EXECUTE THEN
    REPEAT
;
