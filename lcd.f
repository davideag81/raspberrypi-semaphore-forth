\ include gpio.f
\ inlcude time.f

DECIMAL

\ VSS     GND
\ VDD     +5V
\ VO      POT
25 CONSTANT LCDRS      
\ RW      GND
24 CONSTANT LCDE       
12 CONSTANT LCD0      
4  CONSTANT LCD1      
27 CONSTANT LCD2      
16 CONSTANT LCD3      
23 CONSTANT LCD4      
17 CONSTANT LCD5      
18 CONSTANT LCD6      
22 CONSTANT LCD7      
\ A       +5V
\ K       GND

HEX

80 CONSTANT LCDLN1
C0 CONSTANT LCDLN2

: LCDSETUP 
    LCDE  GPFSELOUT!
    LCDRS GPFSELOUT!
    LCD0 GPFSELOUT!
    LCD1 GPFSELOUT!
    LCD2 GPFSELOUT!
    LCD3 GPFSELOUT!
    LCD4 GPFSELOUT!
    LCD5 GPFSELOUT!
    LCD6 GPFSELOUT!
    LCD7 GPFSELOUT!
;


: +LCDE LCDE GPON! ;

: -LCDE LCDE GPOFF! ;

: LCDESIGN 500 USEC DELAY +LCDE 500 USEC DELAY -LCDE 500 USEC DELAY ;

: LCDDOFF!
    LCDE GPOFF!
    LCD0 GPOFF!
    LCD1 GPOFF!
    LCD2 GPOFF!
    LCD3 GPOFF!
    LCD4 GPOFF!
    LCD5 GPOFF!
    LCD6 GPOFF!
    LCD7 GPOFF!
;

: BITSET AND 0<> ;
: ?LCDD SWAP IF GPON! ELSE GPOFF! THEN ;

: LCDWRITE
    DUP 100 BITSET LCDRS ?LCDD

    DUP 80 BITSET LCD7 ?LCDD
    DUP 40 BITSET LCD6 ?LCDD
    DUP 20 BITSET LCD5 ?LCDD
    DUP 10 BITSET LCD4 ?LCDD
    DUP 08 BITSET LCD3 ?LCDD
    DUP 04 BITSET LCD2 ?LCDD
    DUP 02 BITSET LCD1 ?LCDD 
        01 BITSET LCD0 ?LCDD 

    LCDESIGN
;

: LCDWRITE4
    DUP 100 BITSET LCDRS ?LCDD
    
    \ set high bits
    DUP 80 BITSET LCD7 ?LCDD
    DUP 40 BITSET LCD6 ?LCDD
    DUP 20 BITSET LCD5 ?LCDD
    DUP 10 BITSET LCD4 ?LCDD
    
    LCDESIGN
    
    \ set low bits
    DUP 08 BITSET LCD7 ?LCDD
    DUP 04 BITSET LCD6 ?LCDD
    DUP 02 BITSET LCD5 ?LCDD
        01 BITSET LCD4 ?LCDD
    
    LCDESIGN
;

: LCDCLR 1 LCDWRITE ;

: LCDINIT
    33 LCDWRITE
    38 LCDWRITE
    6 LCDWRITE
    C LCDWRITE
    LCDCLR
;


: LCDTYPE OVER + SWAP BEGIN 2DUP <> WHILE dup c@ 100 + LCDWRITE 1+ REPEAT 2DROP ;

: LCDMESSAGE LCDWRITE LCDTYPE ;